apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "\"" + VERSION_NAME + "\""
        buildConfigField 'int', 'VERSION_CODE', "1"
        buildConfigField 'String', 'VERSION_NAME', "\"" + VERSION_NAME + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    libraryVariants.all { variant ->
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."

            /* add the excluded packages */
            exclude "**/R**"
            exclude "**/BuildConfig*"

            classpath = variant.javaCompiler.classpath + files(android.getBootClasspath()) + files("$buildDir/intermediates/classes/$variant.name")

            source = android.sourceSets.main.java.sourceFiles
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = project.file('../docs')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }


    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !ciBuild
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation 'com.squareup.tape2:tape:2.0.0-beta1'
    api 'com.scottyab:rootbeer-lib:0.0.8'
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    api 'androidx.core:core-ktx:1.12.0'
    embed(name: 'highwind-1.4.4', ext: 'aar', version: '1.4.4', group: 'io.castle.android.highwind')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test:rules:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    if (ciBuild) {
        api fileTree(dir: 'libs', include: ['*.aar'])
    }
}

if (!ciBuild) {
    apply from: 'size.gradle'
}

mavenPublish {
    nexus {
        stagingProfile = "io.castle"
    }
}

repositories {
    flatDir {
        dirs 'libs/'
    }
}