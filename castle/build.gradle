apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "\"" + VERSION_NAME + "\""
        buildConfigField 'int', 'VERSION_CODE', "1"
        buildConfigField 'String', 'VERSION_NAME', "\"" + VERSION_NAME + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    libraryVariants.all { variant ->
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."

            /* add the excluded packages */
            exclude "**/R**"
            exclude "**/BuildConfig*"

            classpath = variant.javaCompiler.classpath + files(android.getBootClasspath()) + files("$buildDir/intermediates/classes/$variant.name")

            source = android.sourceSets.main.java.sourceFiles
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = project.file('../docs')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !ciBuild
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.tape2:tape:2.0.0-beta1'
    api 'com.scottyab:rootbeer-lib:0.0.8'
    api 'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'
    embed(name: 'highwind-1.1.0', ext: 'aar')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.7.2'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    if (ciBuild) {
        api fileTree(dir: 'libs', include: ['*.aar'])
    }
}

if (!ciBuild) {
    apply from: 'size.gradle'
}

mavenPublish {
    nexus {
        stagingProfile = "io.castle"
    }
}

repositories {
    flatDir {
        dirs 'libs/'
    }
}