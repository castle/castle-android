apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def POM_VERSION = '1.1.5'
def POM_GROUP_ID = 'io.castle.android'
def POM_ARTIFACT_ID = 'castle'
def POM_DESCRIPTION = 'Android client library for Castle'
def POM_URL = 'https://github.com/castle/castle-android'
def POM_SCM_CONNECTION = 'scm:git:git://github.com/castle/castle-android.git'

def getRepositoryUsername() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getRepositoryApiKey() {
    return hasProperty('BINTRAY_API_KEY') ? BINTRAY_API_KEY : ""
}

def getRepositoryPassword() {
    return hasProperty('BINTRAY_GPG_PASSWORD') ? BINTRAY_GPG_PASSWORD : ""
}

version = POM_VERSION
group = POM_GROUP_ID

bintray {
    user = getRepositoryUsername()
    key = getRepositoryApiKey()
    configurations = ['archives']
    pkg {
        repo = 'castle-android'
        name = POM_ARTIFACT_ID
        desc = POM_DESCRIPTION
        websiteUrl = POM_URL
        vcsUrl = POM_URL
        publish = true
        publicDownloadNumbers = true
        userOrg = 'castleintelligence'
        version {
            desc = POM_DESCRIPTION
            released  = new Date()
            gpg {
                sign = true
                passphrase = getRepositoryPassword()
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'castle-android'
            description POM_DESCRIPTION
            url POM_URL

            packaging 'aar'
            groupId POM_GROUP_ID
            artifactId POM_ARTIFACT_ID
            version POM_VERSION

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }

            scm {
                url POM_URL
                connection POM_SCM_CONNECTION
                developerConnection POM_SCM_CONNECTION
            }

            developers {
                developer {
                    id 'castle'
                    name 'Castle Intelligence, Inc.'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}