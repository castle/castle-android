/*
 * Copyright (c) 2020 Castle
 */

apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.vanniktech.android.apk.size'

tasks.create("depsize-all-configurations") {
    doLast {
        configurations.each {
            if (it.isCanBeResolved()) {
                listConfigurationDependencies(it)
            }
        }
    }
}

tasks.create("depsize") {
    doLast {
        listConfigurationDependencies(configurations.getByName('releaseRuntimeClasspath'))
    }
}

task('size', dependsOn: ['clean', 'assembleRelease', 'countReleaseDexMethods', 'depsize'])

def listConfigurationDependencies(Configuration configuration) {
    def formatStr = "%,10.2f"

    def size = configuration.collect { it.length() / (1024 * 1024) }.sum()

    def out = new StringBuffer()
    out << "\nConfiguration name: \"${configuration.name}\"\n"
    if (size) {
        out << 'Total dependencies size:'.padRight(65)
        out << "${String.format(formatStr, size)} Mb\n\n"

        configuration.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(65)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
    } else {
        out << 'No dependencies found'
    }
    println(out)
}